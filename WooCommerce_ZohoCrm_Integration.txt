string standalone.WooCommerceOrderSync(Map crmAPIRequest)
{
try 
{
	rawBody = crmAPIRequest.get("body");
	if(rawBody == null)
	{
		return "No request Body";
	}
	headersMap = crmAPIRequest.get("headers");
	if(headersMap == null)
	{
		return "Can't verify this webhook";
	}
	signatureHeader = "";
	xWcSigList = headersMap.get("x-wc-webhook-signature");
	if(xWcSigList != null)
	{
		try 
		{
			signatureHeader = xWcSigList.get(0);
		}
		catch (e)
		{
			signatureHeader = xWcSigList.toString();
		}
	}
	if(signatureHeader == "")
	{
		info "Missing webhook signature";
		return "No webhook secret signature";
	}
	woocommerce_secret = zoho.crm.getOrgVariable("WOO_WEBHOOK_SECRET");
	signatureValid = false;
	try 
	{
		computed1 = zoho.encryption.hmacsha256(woocommerce_secret,rawBody);
		computed2 = zoho.encryption.hmacsha256(rawBody,woocommerce_secret);
		if(computed1 == signatureHeader || computed2 == signatureHeader)
		{
			signatureValid = true;
		}
	}
	catch (e)
	{
		info "Signature error: " + e.toString();
	}
	if(!signatureValid)
	{
		info "Invalid signature";
		return "Invalid signature";
	}
	//extract order data
	orderData = rawBody.toMap();
	billing = orderData.get("billing");
	if(billing == null)
	{
		info "No billing data";
		return "No billing data";
	}
	email = billing.get("email");
	if(email == null || email.trim() == "")
	{
		info "Email required";
		return "Email required";
	}
	firstName = billing.get("first_name");
	lastName = billing.get("last_name");
	phone = billing.get("phone");
	company = billing.get("company");
	orderId = orderData.get("id");
	orderNumber = orderData.get("number");
	orderTotal = orderData.get("total");
	orderStatus = orderData.get("status");
	currency = orderData.get("currency");
	lineItems = orderData.get("line_items");
	info "Processing order: " + orderId + " | Number: " + orderNumber + " | Total: " + orderTotal;
	productList = "";
	totalQuantity = 0;
	if(lineItems != null)
	{
		for each  item in lineItems
		{
			productName = item.get("name");
			quantity = item.get("quantity");
			price = item.get("total");
			if(quantity != null)
			{
				try 
				{
					totalQuantity = totalQuantity + quantity.toNumber();
				}
				catch (e)
				{
				}
			}
			productList = productList + "- " + productName + " (Qty: " + quantity + ", Price: " + price + " " + currency + ")\n";
		}
	}
	//Create or update contact if found
	contactMap = Map();
	contactMap.put("First_Name",firstName);
	contactMap.put("Last_Name",lastName);
	contactMap.put("Email",email);
	if(phone != null && phone != "")
	{
		contactMap.put("Phone",phone);
	}
	if(billing.get("address_1") != null && billing.get("address_1") != "")
	{
		contactMap.put("Mailing_Street",billing.get("address_1"));
	}
	if(billing.get("city") != null && billing.get("city") != "")
	{
		contactMap.put("Mailing_City",billing.get("city"));
	}
	if(billing.get("state") != null && billing.get("state") != "")
	{
		contactMap.put("Mailing_State",billing.get("state"));
	}
	if(billing.get("postcode") != null && billing.get("postcode") != "")
	{
		contactMap.put("Mailing_Zip",billing.get("postcode"));
	}
	if(billing.get("country") != null && billing.get("country") != "")
	{
		contactMap.put("Mailing_Country",billing.get("country"));
	}
	// Search for existing contact
	contactId = null;
	try 
	{
		searchResponse = zoho.crm.searchRecords("Contacts","(Email:equals:" + email + ")");
		if(searchResponse != null)
		{
			dataList = searchResponse.get("data");
			if(dataList == null)
			{
				dataList = searchResponse;
			}
			if(dataList != null && dataList.size() > 0)
			{
				contactId = dataList.get(0).get("id");
			}
		}
	}
	catch (e)
	{
		info "Contact search error: " + e.toString();
	}
	// Update or create contact
	if(contactId != null)
	{
		try 
		{
			zoho.crm.updateRecord("Contacts",contactId,contactMap);
			info "Contact updated: " + contactId;
		}
		catch (e)
		{
			info "Contact update error: " + e.toString();
		}
	}
	else
	{
		try 
		{
			createResponse = zoho.crm.createRecord("Contacts",contactMap);
			if(createResponse != null && createResponse.get("status") == "success")
			{
				contactId = createResponse.get("details").get("id");
				info "Contact created: " + contactId;
			}
		}
		catch (e)
		{
			info "Contact create error: " + e.toString();
		}
	}
	// DEAL CREATION
	companyName = company;
	if(companyName == null || companyName == "")
	{
		companyName = firstName + " " + lastName;
	}
	if(companyName == null || companyName.trim() == "")
	{
		companyName = "WooCommerce";
	}
	description = "WooCommerce Order #" + orderNumber + " (ID: " + orderId + ")\n";
	description = description + "Order Status: " + orderStatus + "\n";
	description = description + "Total Amount: " + orderTotal + " " + currency + "\n";
	description = description + "Total Items: " + totalQuantity + "\n\n";
	description = description + "Products Ordered:\n" + productList;
	leadMap = Map();
	leadMap.put("First_Name",firstName);
	leadMap.put("Last_Name",lastName);
	leadMap.put("Email",email);
	leadMap.put("Company",companyName);
	leadMap.put("Lead_Source","WooCommerce");
	leadMap.put("Lead_Status","Not Contacted");
	leadMap.put("Description",description);
	if(phone != null && phone != "")
	{
		leadMap.put("Phone",phone);
	}
	if(orderTotal != null)
	{
		try 
		{
			leadMap.put("Annual_Revenue",orderTotal.toDecimal());
		}
		catch (e)
		{
		}
	}
	leadId = null;
	try 
	{
		leadResponse = zoho.crm.createRecord("Leads",leadMap);
		if(leadResponse != null && leadResponse.get("status") == "success")
		{
			leadId = leadResponse.get("details").get("id");
			info "Lead created: " + leadId;
		}
	}
	catch (e)
	{
		info "Lead create error: " + e.toString();
	}
	finalResult = Map();
	finalResult.put("status","success");
	finalResult.put("contact_id",contactId);
	finalResult.put("lead_id",leadId);
	finalResult.put("order_id",orderId);
	finalResult.put("order_number",orderNumber);
	info finalResult;
	return finalResult;
}
catch (ex)
{
	errorResult = Map();
	errorResult.put("status","error");
	errorResult.put("message",ex.toString());
	info errorResult;
	return errorResult;
}
return "Integration completed";
}